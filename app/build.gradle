plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {

    with commonConfig

    defaultConfig {
        applicationId "com.buddies"
    }

    signingConfigs {
        release {
            storeFile file(KEYSTORE_PATH)
            storePassword KEYSTORE_PASSWORD
            keyAlias KEYSTORE_KEY_ALIAS
            keyPassword KEYSTORE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "mode"
    productFlavors {
        generator {
            versionNameSuffix "-generator"
        }
        production {

        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Projects
    implementation project(":common")
    implementation project(":configuration")
    implementation project(":gallery")
    implementation project(":home")
    implementation project(":login")
    implementation project(':missing-all')
    implementation project(':missing-feed')
    implementation project(":missing-profile")
    implementation project(":missing-new")
    implementation project(":mypets")
    implementation project(":navigation")
    implementation project(':newpet')
    implementation project(":notification")
    implementation project(":pet")
    implementation project(":profile")
    implementation project(":scanner")
    implementation project(":security")
    implementation project(":server")
    implementation project(":settings")
    generatorImplementation project(":generator")

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    // AndroidX Core & UI
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.core:core-ktx:$corektx_version"
    implementation "com.google.android.material:material:$material_version"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:$firebase_bom_version")
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // Koin
    implementation "io.insert-koin:koin-android:$koin_version"

    // Preference
    implementation "androidx.preference:preference-ktx:$preference_version"

    // Jetpack Startup
    implementation "androidx.startup:startup-runtime:$startup_version"

    // Testing
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$android_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$expresso_version"
}
